plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.github.pjfanning'
version = '1.0.0-SNAPSHOT'

description = 'Memory efficient Shared String Table for POI'

repositories {
    mavenCentral()
    maven {
        url "https://repository.apache.org/content/repositories/staging/"
    }
}

dependencies {
    compile 'com.h2database:h2:1.4.197'
    compile 'org.apache.poi:poi-ooxml:4.0.0'
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.github.pjfanning'
            artifactId 'poi-shared-strings'
            version '1.0.0-SNAPSHOT'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Memory efficient Shared Strings Table implementation for POI streaming'
            name 'POI Shared Strings'
            url 'https://github.com/pjfanning/poi-shared-strings'
            organization {
                name 'com.github.pjfanning'
                url 'https://github.com/pjfanning'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/pjfanning/poi-shared-strings/issues'
            }
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                url 'https://github.com/pjfanning/poi-shared-strings'
                connection 'scm:git:git://github.com/pjfanning/poi-shared-strings.git'
                developerConnection 'scm:git:ssh://git@github.com:pjfanning/poi-shared-strings.git'
            }
            developers {
                developer {
                    id 'pjfanning'
                    name 'PJ Fanning'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}